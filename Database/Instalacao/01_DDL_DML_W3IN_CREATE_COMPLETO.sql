PRINT N'Creating [W3IN]...';


GO
CREATE SCHEMA [W3IN]
    AUTHORIZATION [dbo];


GO
PRINT N'Creating [W3IN].[CONFIGURACAO_INTEGRACAO]...';


GO
CREATE TABLE [W3IN].[CONFIGURACAO_INTEGRACAO] (
    [COD_CONFIGURACAO_INTEGRACAO] INT            IDENTITY (1, 1) NOT NULL,
    [COD_TIPO_INTEGRACAO]         INT            NOT NULL,
    [ID_CONFIGURACAO_INTEGRACAO]  VARCHAR (100)  NOT NULL,
    [VLR_CONFIGURACAO_INTEGRACAO] VARCHAR (4000) NOT NULL,
    [DAT_ALTERACAO]               DATETIME       NOT NULL,
    CONSTRAINT [PK_CONFINTE] PRIMARY KEY CLUSTERED ([COD_CONFIGURACAO_INTEGRACAO] ASC)
);

GO
PRINT N'Creating [W3IN].[CONFIGURACAO_API]...';


GO
CREATE TABLE [W3IN].[CONFIGURACAO_API] (
		[COD_API]						INT NOT NULL,
        [COD_MODULO]					INT NOT NULL,
		[COD_REGRA_VER_AVALIACAO]		INT NULL,
		[IND_TIPO_VALIDACAO_AC]			CHAR(1) NULL,
		[IND_NOTA_ORIGEM_AC]			CHAR(1) NULL,
		[IND_NOTA_ORIGEM_RC]			CHAR(1) NULL,
		[IND_NOTA_ORIGEM_ME]			CHAR(1) NULL,
		
		[IND_EXPIRACAO_RESULTADOS]		CHAR(1) NOT NULL
			CONSTRAINT [DF_CONFIAPI_IND_EXPIRACAO_RESULTADOS]
				DEFAULT 'N'
            CONSTRAINT [CK_CONFIAPI_IND_EXPIRACAO_RESULTADOS]
                CHECK ([IND_EXPIRACAO_RESULTADOS] IN ('S', 'N')),
		[IND_ANO_REFERENCIA]			CHAR(1) NOT NULL
			CONSTRAINT [DF_CONFIAPI_IND_ANO_REFERENCIA]
				DEFAULT 'N'
            CONSTRAINT [CK_CONFIAPI_IND_ANO_REFERENCIA]
                CHECK ([IND_ANO_REFERENCIA] IN ('S', 'N')),
		[IND_RANGE_ESPECIFICO]			CHAR(1) NOT NULL
			CONSTRAINT [DF_CONFIAPI_IND_RANGE_ESPECIFICO]
				DEFAULT 'N'
            CONSTRAINT [CK_CONFIAPI_IND_RANGE_ESPECIFICO]
                CHECK ([IND_RANGE_ESPECIFICO] IN ('S', 'N')),
		[QTD_DIAS_EXPIRACAO]			INT NULL,
		[NUM_ANO_REFERENCIA]			INT NULL,
		[VLR_MINIMO_RANGE]				FLOAT NULL,
		[VLR_MAXIMO_RANGE]				FLOAT NULL,
        CONSTRAINT [PK_CONFIAPI] PRIMARY KEY CLUSTERED ([COD_API] ASC, [COD_MODULO] ASC),
		CONSTRAINT [FK_CONFIAPI_MODULO] FOREIGN KEY ([COD_MODULO]) 
			REFERENCES [W3SI].[MODULO] ([COD_MODULO]) ON DELETE CASCADE,
		CONSTRAINT [CK_CONFIAPI_IND_TIPO_VALIDACAO_AC] CHECK ([IND_TIPO_VALIDACAO_AC] IN ('C', 'V', 'R')),
		CONSTRAINT [CK_CONFIAPI_IND_EIXO_ORIGEM_AC] CHECK ([IND_NOTA_ORIGEM_AC] IN ('X', 'Y', 'Z')),
		CONSTRAINT [CK_CONFIAPI_IND_EIXO_ORIGEM_RC] CHECK ([IND_NOTA_ORIGEM_RC] IN ('X', 'Y')),
		CONSTRAINT [CK_CONFIAPI_IND_EIXO_ORIGEM_ME] CHECK ([IND_NOTA_ORIGEM_ME] IN ('C'))
);

CREATE INDEX [IDX_CONFIAPI_001] ON [W3IN].[CONFIGURACAO_API] 
(
	[COD_MODULO] ASC
)

CREATE INDEX [IDX_CONFIAPI_002] ON [W3IN].[CONFIGURACAO_API] 
(
	[COD_REGRA_VER_AVALIACAO] ASC
)

GO
PRINT N'Creating [W3IN].[MODELO_CONFIGURACAO_API]...';


GO
CREATE TABLE [W3IN].[MODELO_CONFIGURACAO_API] (
        [COD_API]				INT NOT NULL,
		[COD_MODULO]			INT NOT NULL,
		[COD_MODELO]			INT NOT NULL,
		CONSTRAINT [PK_MOCONAPI] PRIMARY KEY CLUSTERED ([COD_API] ASC, [COD_MODULO] ASC, [COD_MODELO] ASC),
		CONSTRAINT [FK_MOCONAPI_MODULO] FOREIGN KEY ([COD_MODULO]) 
			REFERENCES [W3SI].[MODULO] ([COD_MODULO]) ON DELETE CASCADE,
);

CREATE INDEX [IDX_MOCONAPI_001] ON [W3IN].[MODELO_CONFIGURACAO_API] 
(
	[COD_MODULO] ASC
)

CREATE INDEX [IDX_MOCONAPI_002] ON [W3IN].[MODELO_CONFIGURACAO_API] 
(
	[COD_MODELO] ASC
)

GO
PRINT N'Creating [W3IN].[TIPO_INTEGRACAO]...';


GO
CREATE TABLE [W3IN].[TIPO_INTEGRACAO] (
    [COD_TIPO_INTEGRACAO] INT           NOT NULL,
    [NOM_TIPO_INTEGRACAO] VARCHAR (255) NOT NULL,
    CONSTRAINT [PK_TIPOINTE] PRIMARY KEY CLUSTERED ([COD_TIPO_INTEGRACAO] ASC)
);


GO
PRINT N'Creating [W3IN].[FK_TIPOINTE_CONFINTE_01]...';


GO
ALTER TABLE [W3IN].[CONFIGURACAO_INTEGRACAO]
    ADD CONSTRAINT [FK_TIPOINTE_CONFINTE_01] FOREIGN KEY ([COD_TIPO_INTEGRACAO]) REFERENCES [W3IN].[TIPO_INTEGRACAO] ([COD_TIPO_INTEGRACAO]);


GO
PRINT N'Creating [W3IN].[FN_VERSAO_IN]...';


GO
CREATE FUNCTION W3IN.FN_VERSAO_IN()
	RETURNS VARCHAR(20)
BEGIN
	RETURN '2025.1.0'
END
GO

GO
CREATE FUNCTION W3IN.FN_CALCULAR_MEDIA_RELATIVA (@VLR_NOTA FLOAT, @MIN_NOTA FLOAT, @MAX_NOTA FLOAT, @MIN_RELATIVO FLOAT, @MAX_RELATIVO FLOAT)
	RETURNS DECIMAL(10, 2)
AS  
BEGIN
	DECLARE @VLR_RELATIVO FLOAT
	
	IF @MAX_NOTA = @MIN_NOTA
		SET @VLR_RELATIVO = @MAX_NOTA
	ELSE
		SET @VLR_RELATIVO = @MIN_RELATIVO + (((@VLR_NOTA - @MIN_NOTA) * (@MAX_RELATIVO - @MIN_RELATIVO)) / (@MAX_NOTA - @MIN_NOTA))

	RETURN CONVERT(DECIMAL(10, 2), @VLR_RELATIVO)
END
GO

DECLARE 
	@codModulo INT = 30

-- DELETE
DELETE FROM W3SI.PARAMETRO WHERE COD_MODULO = @codModulo
DELETE FROM W3SI.SUBMODULO WHERE COD_MODULO = @codModulo
DELETE FROM W3SI.MODULO WHERE COD_MODULO = @codModulo

-- MODULO
INSERT INTO W3SI.MODULO (COD_MODULO, SGL_MODULO, IND_TIPO_MODULO, NOM_MODULO, NOM_USUARIO_BANCO, DSC_URL) VALUES (@codModulo, 'IN', 'S', 'Integrador', 'W3IN', 'LG.HCM.Integrador/')

-- SUBMODULOS
INSERT INTO W3SI.SUBMODULO VALUES(3001, @codModulo, 'TR', 'M', 'Gen.te Aprende – Treinamento', 'IntegracaoLms.aspx', 'S', 'N', 'N')
INSERT INTO W3SI.SUBMODULO VALUES(3002, @codModulo, 'CL', 'M', 'Fit de Comportamentos Clave ', 'IntegracaoClave.aspx', 'S', 'N', 'N')

-- PARAMETROS
DECLARE 
	@urlSI VARCHAR(4000)

SELECT @urlSI = VLR_PARAMETRO FROM W3SI.PARAMETRO WHERE COD_MODULO IS NULL AND NOM_PARAMETRO ='UrlSI'

SELECT * INTO W3SI.TMP_PARAMETRO FROM W3SI.PARAMETRO WHERE 1 = 2

-- Parametros Integração LMS
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'IntegracaoTreinamento.EnvioDadosCorporativos', 'Indica se o envio de dados corporativos para o sistema Gen.te Aprende - Treinamento está habilitado.', 'FALSE', 'TRUE, Sim;FALSE, Não')
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'IntegracaoTreinamento.TokenClientId', 'Valor do client_id fornecido pelo sistema Gen.te Aprende - Treinamento, para permitir a autenticaçao Oauth2 nas API''s do sistema.', 'client_id', NULL)
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'C', 'IntegracaoTreinamento.TokenClientSecret', 'Valor do client_secret fornecido pelo sistema Gen.te Aprende - Treinamento, para permitir a autenticaçao Oauth2 nas API''s do sistema.', 'r0q7qBU2681jrAwuaojKHPDHJ7sLyNUMHBNlU2BKV3M=', NULL)
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'IntegracaoTreinamento.UrlBaseApi', 'Indica a URL base para acesso as API''s do sistema Gen.te Aprende - Treinamento.', 'https://localhost/webapi/', NULL)

-- Parametros Integração Clave
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'C', 'IntegracaoClave.Chave', 'Indica se o envio de dados corporativos para o sistema da Clave está habilitado.', 'r0q7qBU2681jrAwuaojKHPDHJ7sLyNUMHBNlU2BKV3M=', NULL)
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'IntegracaoClave.EnvioDadosCorporativos', 'Indica se o envio de dados corporativos para o sistema da Clave está habilitado.', 'FALSE', 'TRUE, Sim;FALSE, Não')
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'IntegracaoClave.UrlBase', 'Indica a URL base para acesso as APIs e ao login SSO, do sistema Fit de Comportamentos', 'https://localhost/webapi/', NULL)

-- Demais parâmetros
INSERT INTO W3SI.TMP_PARAMETRO (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) VALUES ('A', @codModulo, 'S', 'Url', 'URL do Modulo. A URL deve conter a barra "/" no final.', @urlSI + 'Modulos/LG.HCM.Integrador/', NULL)

MERGE W3SI.PARAMETRO AS TARGET
USING W3SI.TMP_PARAMETRO AS SOURCE 
ON (TARGET.COD_MODULO = 30 AND SOURCE.COD_MODULO = 30 AND TARGET.NOM_PARAMETRO = SOURCE.NOM_PARAMETRO)
WHEN MATCHED AND
	TARGET.IND_ESCOPO <> SOURCE.IND_ESCOPO
	OR TARGET.IND_TIPO_VALOR <> SOURCE.IND_TIPO_VALOR
	OR TARGET.NOM_PARAMETRO <> SOURCE.NOM_PARAMETRO
	OR TARGET.DSC_PARAMETRO <> SOURCE.DSC_PARAMETRO
	OR TARGET.DSC_OPCAO <> SOURCE.DSC_OPCAO
THEN 
	UPDATE SET
		TARGET.IND_ESCOPO = SOURCE.IND_ESCOPO,
		TARGET.IND_TIPO_VALOR = SOURCE.IND_TIPO_VALOR,
		TARGET.NOM_PARAMETRO = SOURCE.NOM_PARAMETRO,
		TARGET.DSC_PARAMETRO = SOURCE.DSC_PARAMETRO,
		TARGET.DSC_OPCAO = SOURCE.DSC_OPCAO
WHEN NOT MATCHED BY TARGET THEN
	INSERT (IND_ESCOPO, COD_MODULO, IND_TIPO_VALOR, NOM_PARAMETRO, DSC_PARAMETRO, VLR_PARAMETRO, DSC_OPCAO) 
	VALUES (SOURCE.IND_ESCOPO, SOURCE.COD_MODULO, SOURCE.IND_TIPO_VALOR, SOURCE.NOM_PARAMETRO, SOURCE.DSC_PARAMETRO, SOURCE.VLR_PARAMETRO, SOURCE.DSC_OPCAO);

--VINCULA OS PARAMETROS PARA OS PERFIS
SELECT * INTO W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO FROM W3SI.PERFIL_ADMINISTRADOR_PARAMETRO WHERE 1 = 2

DECLARE 
	@codPerfilAdminLG INT = 1,
	@codPerfilAdminGeral INT = 2,
	@codPerfilAdminAuxiliar INT = 3,
	@codPerfilConsultor INT = 4

--INSERE TODOS OS PARAMETROS PARA O ADMINISTRADOR LG
INSERT INTO W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO (COD_PERFIL_ADMINISTRADOR, COD_PARAMETRO)
	SELECT @codPerfilAdminLG, COD_PARAMETRO 
	FROM W3SI.PARAMETRO P
	WHERE COD_MODULO = @codModulo

--INSERE TODOS OS PARAMETROS PARA O ADMINISTRADOR GERAL
INSERT INTO W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO (COD_PERFIL_ADMINISTRADOR, COD_PARAMETRO)
	SELECT @codPerfilAdminGeral, COD_PARAMETRO 
	FROM W3SI.PARAMETRO P
	WHERE COD_MODULO = @codModulo

--INSERE TODOS OS PARAMETROS PARA O CONSULTOR
INSERT INTO W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO (COD_PERFIL_ADMINISTRADOR, COD_PARAMETRO)
	SELECT @codPerfilConsultor, COD_PARAMETRO 
	FROM W3SI.PARAMETRO P
	WHERE COD_MODULO = @codModulo

--APLICA O MERGE
MERGE W3SI.PERFIL_ADMINISTRADOR_PARAMETRO AS TARGET
USING W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO AS SOURCE 
ON (TARGET.COD_PERFIL_ADMINISTRADOR = SOURCE.COD_PERFIL_ADMINISTRADOR AND TARGET.COD_PARAMETRO = SOURCE.COD_PARAMETRO)
WHEN NOT MATCHED BY TARGET AND SOURCE.COD_PARAMETRO IN (SELECT COD_PARAMETRO FROM W3SI.PARAMETRO WHERE COD_MODULO = @codModulo) THEN
	INSERT (COD_PERFIL_ADMINISTRADOR, COD_PARAMETRO) 
	VALUES (SOURCE.COD_PERFIL_ADMINISTRADOR, SOURCE.COD_PARAMETRO)
WHEN NOT MATCHED BY SOURCE AND TARGET.COD_PARAMETRO IN (SELECT COD_PARAMETRO FROM W3SI.PARAMETRO WHERE COD_MODULO = @codModulo) THEN
DELETE;

DROP TABLE W3SI.TMP_PERFIL_ADMINISTRADOR_PARAMETRO
DROP TABLE W3SI.TMP_PARAMETRO
GO

-- TIPO DE INTEGRAÇÃO
INSERT INTO W3IN.TIPO_INTEGRACAO (COD_TIPO_INTEGRACAO, NOM_TIPO_INTEGRACAO) VALUES (1, 'Autoatendimento')
INSERT INTO W3IN.TIPO_INTEGRACAO (COD_TIPO_INTEGRACAO, NOM_TIPO_INTEGRACAO) VALUES (2, 'Autenticação Integrada')

-- CONFIGURACAO_INTEGRACAO
-- Autoatendimento
INSERT INTO W3IN.CONFIGURACAO_INTEGRACAO (COD_TIPO_INTEGRACAO, ID_CONFIGURACAO_INTEGRACAO, VLR_CONFIGURACAO_INTEGRACAO, DAT_ALTERACAO) VALUES (1, 'UrlIntegracaoSA3', 'https://login.lg.com.br/api.autoatendimento/token', getdate())
INSERT INTO W3IN.CONFIGURACAO_INTEGRACAO (COD_TIPO_INTEGRACAO, ID_CONFIGURACAO_INTEGRACAO, VLR_CONFIGURACAO_INTEGRACAO, DAT_ALTERACAO) VALUES (1, 'CiclosCompetenciasAutoatendimento', '', getdate())
-- Autenticação Integrada
INSERT INTO W3IN.CONFIGURACAO_INTEGRACAO (COD_TIPO_INTEGRACAO, ID_CONFIGURACAO_INTEGRACAO, VLR_CONFIGURACAO_INTEGRACAO, DAT_ALTERACAO) VALUES (2, 'UrlBase', 'https://login.lg.com.br', getdate())
INSERT INTO W3IN.CONFIGURACAO_INTEGRACAO (COD_TIPO_INTEGRACAO, ID_CONFIGURACAO_INTEGRACAO, VLR_CONFIGURACAO_INTEGRACAO, DAT_ALTERACAO) VALUES (2, 'ChavePublica', 'ec0e957c-c0d7-4284-b557-88ba6610ea1d', getdate())
INSERT INTO W3IN.CONFIGURACAO_INTEGRACAO (COD_TIPO_INTEGRACAO, ID_CONFIGURACAO_INTEGRACAO, VLR_CONFIGURACAO_INTEGRACAO, DAT_ALTERACAO) VALUES (2, 'ChavePrivada', '9bc85c98-9bae-4446-b1d8-4004ebeab5a4', getdate())
GO

PRINT N'Update complete.';
GO
