BEGIN TRAN

DECLARE @codModulo INT
SET @codModulo = 30

-- REMOVE AS MENSAGENS DUPLICADAS, MANTENDO O ÚLTIMO REGISTRO QUE FOI INSERIDO
PRINT 'Excluindo mensagens duplicadas...'

DECLARE @ocorrencias INT
SET @ocorrencias = 1

WHILE @ocorrencias > 0 BEGIN
	DELETE FROM W3SI.MENSAGEM
	WHERE COD_MENSAGEM IN 
	(
		SELECT MIN(M.COD_MENSAGEM) AS COD_MENSAGEM
		FROM W3SI.MENSAGEM M,
			(SELECT M.COD_MODULO, M.COD_IDIOMA, COALESCE(M.NOM_PAGINA, '-') AS NOM_PAGINA, COALESCE(M.NOM_CONTROLE, '-') AS NOM_CONTROLE, COALESCE(M.NOM_PROPRIEDADE, '-') AS NOM_PROPRIEDADE, COALESCE(M.NOM_CHAVE, '-') AS NOM_CHAVE, M.IND_REVISADA, COUNT(*) AS QUANTIDADE
			FROM W3SI.MENSAGEM M
			WHERE M.COD_MODULO = @codModulo
			GROUP BY M.COD_MODULO, M.COD_IDIOMA, COALESCE(M.NOM_PAGINA, '-'), COALESCE(M.NOM_CONTROLE, '-'), COALESCE(M.NOM_PROPRIEDADE, '-'), COALESCE(M.NOM_CHAVE, '-'), M.IND_REVISADA
			HAVING COUNT(*) > 1) X
		WHERE M.COD_MODULO = X.COD_MODULO AND
			M.COD_IDIOMA = X.COD_IDIOMA AND
			M.IND_REVISADA = X.IND_REVISADA AND 
			COALESCE(M.NOM_PAGINA, '-') = COALESCE(X.NOM_PAGINA, '-') AND
			COALESCE(M.NOM_CONTROLE, '-') = COALESCE(X.NOM_CONTROLE, '-') AND
			COALESCE(M.NOM_PROPRIEDADE, '-') = COALESCE(X.NOM_PROPRIEDADE, '-') AND
			COALESCE(M.NOM_CHAVE, '-') = COALESCE(X.NOM_CHAVE, '-')
		GROUP BY M.COD_MODULO, M.COD_IDIOMA, COALESCE(M.NOM_PAGINA, '-'), COALESCE(M.NOM_CONTROLE, '-'), COALESCE(M.NOM_PROPRIEDADE, '-'), COALESCE(M.NOM_CHAVE, '-')
	)	

	SET @ocorrencias = @@ROWCOUNT
END

DELETE FROM W3SI.MENSAGEM
WHERE COD_MENSAGEM IN (
	SELECT M1.COD_MENSAGEM
	FROM W3SI.MENSAGEM M1
		INNER JOIN W3SI.MENSAGEM M2 ON M1.COD_MODULO = M2.COD_MODULO AND
			M1.COD_IDIOMA = M2.COD_IDIOMA AND
			COALESCE(M1.NOM_PAGINA, '-') = COALESCE(M2.NOM_PAGINA, '-') AND
			COALESCE(M1.NOM_CONTROLE, '-') = COALESCE(M2.NOM_CONTROLE, '-') AND
			COALESCE(M1.NOM_PROPRIEDADE, '-') = COALESCE(M2.NOM_PROPRIEDADE, '-') AND
			COALESCE(M1.NOM_CHAVE, '-') = COALESCE(M2.NOM_CHAVE, '-')
	WHERE M1.COD_MODULO = @codModulo AND
		M1.IND_REVISADA = 'N' AND
		M2.IND_REVISADA = 'S'
)

-- INSERE AS MENSAGENS NÃO EXISTENTES
PRINT CHAR(13) + 'Inserindo as novas mensagens...'

INSERT INTO W3SI.MENSAGEM (COD_IDIOMA, COD_MODULO, NOM_PAGINA, NOM_CONTROLE, NOM_PROPRIEDADE, NOM_CHAVE, DSC_MENSAGEM)
SELECT COD_IDIOMA, COD_MODULO, NOM_PAGINA, NOM_CONTROLE, NOM_PROPRIEDADE, NOM_CHAVE, DSC_MENSAGEM
FROM W3IN.MENSAGEM_TMP T
WHERE T.COD_MODULO = @codModulo AND
	NOT EXISTS (SELECT 1 
		FROM W3SI.MENSAGEM M
		WHERE M.COD_MODULO = T.COD_MODULO AND
			M.COD_IDIOMA = T.COD_IDIOMA AND
			COALESCE(M.NOM_PAGINA, '-') = COALESCE(T.NOM_PAGINA, '-') AND
			COALESCE(M.NOM_CONTROLE, '-') = COALESCE(T.NOM_CONTROLE, '-') AND
			COALESCE(M.NOM_PROPRIEDADE, '-') = COALESCE(T.NOM_PROPRIEDADE, '-') AND
			COALESCE(M.NOM_CHAVE, '-') = COALESCE(T.NOM_CHAVE, '-'))

-- ATUALIZA AS MENSAGENS JÁ EXISTENTES		
PRINT CHAR(13) + 'Atualizando as mensagens existentes que não foram revisadas...'

UPDATE M
SET DSC_MENSAGEM = T.DSC_MENSAGEM
FROM W3SI.MENSAGEM M, W3IN.MENSAGEM_TMP T
WHERE M.COD_MODULO = @codModulo AND
	M.IND_REVISADA = 'N' AND
	M.COD_MODULO = T.COD_MODULO AND
	M.COD_IDIOMA = T.COD_IDIOMA AND
	COALESCE(M.NOM_PAGINA, '-') = COALESCE(T.NOM_PAGINA, '-') AND
	COALESCE(M.NOM_CONTROLE, '-') = COALESCE(T.NOM_CONTROLE, '-') AND
	COALESCE(M.NOM_PROPRIEDADE, '-') = COALESCE(T.NOM_PROPRIEDADE, '-') AND
	COALESCE(M.NOM_CHAVE, '-') = COALESCE(T.NOM_CHAVE, '-')

-- EXCLUI AS MENSAGENS QUE NÃO EXISTEM MAIS
-- UTILIZAR O SCRIPT ABAIXO APENAS QUANDO FOR NECESSÁRIO (P.EX. MIGRAÇÃO AC 2008.3 > 2010.1
/*
PRINT CHAR(13) + 'Excluindo mensagens antigas...'

DELETE FROM W3SI.MENSAGEM
WHERE COD_MENSAGEM IN
	(SELECT M.COD_MENSAGEM
	FROM W3SI.MENSAGEM M
		LEFT JOIN W3IN.MENSAGEM_TMP T ON M.COD_MODULO = T.COD_MODULO AND
			M.COD_IDIOMA = T.COD_IDIOMA AND
			COALESCE(M.NOM_PAGINA, '-') = COALESCE(T.NOM_PAGINA, '-') AND
			COALESCE(M.NOM_CONTROLE, '-') = COALESCE(T.NOM_CONTROLE, '-') AND
			COALESCE(M.NOM_PROPRIEDADE, '-') = COALESCE(T.NOM_PROPRIEDADE, '-') AND
			COALESCE(M.NOM_CHAVE, '-') = COALESCE(T.NOM_CHAVE, '-')
	WHERE M.COD_MODULO = @codModulo
		AND T.COD_MENSAGEM IS NULL)
*/

COMMIT TRAN